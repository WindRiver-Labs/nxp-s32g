#@TYPE: Machine
#@NAME: nxp-s32g2xx
#@DESCRIPTION: Machine configuration for NXP S32G274A SoC

DEFAULTTUNE = "cortexa53"
require conf/machine/include/tune-cortexa53.inc

MACHINE_FEATURES = "ext3 pci usbgadget usbhost vfat"

KERNEL_IMAGETYPE = ""
KERNEL_IMAGETYPES = " ${@bb.utils.contains('UBOOT_SIGN_ENABLE', '1', 'fitImage', 'Image.gz Image', d)}"
KERNEL_DEVICETREE = "freescale/fsl-s32g274a-evb.dtb \
                     freescale/fsl-s32g274a-rdb2.dtb"

SERIAL_CONSOLES ?= "115200;ttyLF0"
SERIAL_CONSOLES_CHECK ?= "${SERIAL_CONSOLES}"

IMAGE_FSTYPES += "tar.bz2"

# multilib support
MULTILIBS ?= "multilib:lib32"
DEFAULTTUNE_virtclass-multilib-lib32 ?= "armv7athf-neon"
EXTRA_IMAGECMD_jffs2 = "--pad --no-cleanmarkers --eraseblock=128"

UBOOT_CONFIG ??= "s32g274ardb2 s32g274aevb"

UBOOT_CONFIG[s32g274aevb] = "s32g2xxaevb_defconfig"
UBOOT_CONFIG[s32g274ardb2] = "s32g274ardb2_defconfig"

UBOOT_MAKE_TARGET ?= "u-boot.s32"
UBOOT_BINARY = "u-boot.s32"
PREFERRED_VERSION_u-boot-s32 ?= "2020.04"

WIC_CREATE_EXTRA_ARGS = "--no-fstab-update"
MACHINE_EXTRA_RRECOMMENDS = " kernel-modules kernel-devicetree"
IMAGE_FSTYPES += "tar.bz2 wic wic.bmap"
WKS_FILE ?= "s32g2xx-sdimage-bootpart.wks"
IMAGE_INSTALL_append = " ${@bb.utils.contains('UBOOT_SIGN_ENABLE', '1', 'kernel-devicetree kernel-image-fitimage', 'kernel-devicetree kernel-image-image', d)}"
do_image_wic[depends] += "mtools-native:do_populate_sysroot dosfstools-native:do_populate_sysroot"
IMAGE_BOOT_FILES ?= " ${@bb.utils.contains('UBOOT_SIGN_ENABLE', '1', 'fitImage', 'Image fsl-s32g274a-evb.dtb fsl-s32g274a-rdb2.dtb', d)}"
IMAGE_BOOT_FILES_append = " ${@bb.utils.contains('IMAGE_INSTALL', 'pfe', 's32g_pfe_class.fw s32g_pfe_util.fw', '', d)}"

# Enable ATF
require ${@bb.utils.contains('IMAGE_INSTALL', 'arm-trusted-firmware', 'conf/machine/include/atf.inc', '', d)}
PREFERRED_PROVIDER_virtual/arm-trusted-firmware ?= " ${@bb.utils.contains('IMAGE_INSTALL', 'arm-trusted-firmware', 'arm-trusted-firmware', '', d)}"
PREFERRED_PROVIDER_virtual/kernel ??= "linux-yocto"
PREFERRED_VERSION_linux-yocto ?= "5.4%"

